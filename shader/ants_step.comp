#version 460

#extension GL_GOOGLE_include_directive: require
#include "ants.glsl"
#include "random.glsl"

layout(constant_id = 0) const uint ANT_COUNT = 1;

layout(binding = 0) readonly buffer AntBufferIn {
	Ant ants_in[];
};

layout(binding = 1) writeonly buffer AntBufferOut {
	Ant ants_out[];
};

struct PushConstants
{
	uint frame_idx;
	float frame_time;
	float total_time;
};

layout(push_constant) uniform PushConstant { PushConstants pc; };

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

void main()
{
	uint thread_id = gl_GlobalInvocationID.x;
	if (thread_id >= ANT_COUNT) return;
	rng_state = (thread_id + ANT_COUNT * pc.frame_idx);
	Ant ant = ants_in[thread_id];
	// sample a new direction if ant has none or if some amount of time passed with some probability
	const float time_to_new_step = 0.1;
	uint prev_steps_passed = uint((pc.total_time - pc.frame_time) / time_to_new_step);
	uint steps_passed = uint(pc.total_time / time_to_new_step);
	const bool sample_new_direction = dot(ant.dir, ant.dir) == 0.0 || ((steps_passed != prev_steps_passed) && (pcg_random_state() < 0.01));
	if (sample_new_direction) ant.dir = vec2(pcg_random_state(-1.0, 1.0), pcg_random_state(-1.0, 1.0));
	ant.dir += vec2(pcg_random_state(-0.01, 0.01), pcg_random_state(-0.01, 0.01));
	ant.dir = normalize(ant.dir) / 10.0;
	ant.pos += ant.dir * pc.frame_time;
	if (ant.pos.x > 1.0)
	{
		ant.pos.x = 1.0;
		ant.dir.x = -ant.dir.x * 0.1;
	}
	if (ant.pos.x < -1.0)
	{
		ant.pos.x = -1.0;
		ant.dir.x = -ant.dir.x * 0.1;
	}
	if (ant.pos.y > 1.0)
	{
		ant.pos.y = 1.0;
		ant.dir.y = -ant.dir.y * 0.1;
	}
	if (ant.pos.y < -1.0)
	{
		ant.pos.y = -1.0;
		ant.dir.y = -ant.dir.y * 0.1;
	}
	ants_out[thread_id] = ant;
}
