#version 460

#extension GL_GOOGLE_include_directive: require
#include "hash_grid_struct.glsl"

layout(constant_id = 0) const uint HASH_GRID_CAPACITY = 1;

layout(binding = 0) buffer HashGridBuffer {
	HashGridCell hash_grid[];
};

#include "hash_grid_functions.glsl"

struct PushConstants
{
	vec2 food_pos;
	int food_amount;
	uint frame_idx;
	float frame_time;
	float total_time;
};

layout(push_constant) uniform PushConstant { PushConstants pc; };

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

void main()
{
	const uint id = gl_GlobalInvocationID.x;
	if (!is_valid_hash_grid(id)) return;
	ivec2 food_hash_grid_pos = get_hash_grid_pos(pc.food_pos);
	const uint food_slot = get_hash_grid_slot(food_hash_grid_pos);
	if (id == food_slot && pc.food_amount > 0)
	{
		hash_grid[food_slot].food_amount = pc.food_amount;
		hash_grid[food_slot].state_bits |= HASH_GRID_ACTIVE;
		hash_grid[food_slot].index = food_hash_grid_pos;
	}
	const bool cell_inactive = hash_grid[id].food_amount == 0 && hash_grid[id].pheromone_lifetime > PHEROMONE_LIFETIME;
	if (cell_inactive) hash_grid[id].state_bits = 0u;
	else
	{
		hash_grid[id].state_bits &= ~HASH_GRID_LOCKED;
		hash_grid[id].pheromone_lifetime += pc.frame_time;
	}
}
